plugins {
    id 'java'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.openapi.generator)
}

group = 'dev.minitrello'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

processResources {
    dependsOn(tasks.openApiGenerate)
}

processTestResources {
    dependsOn(tasks.openApiGenerate)
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = "$rootDir/api/openapi.yaml"
    outputDir = "$rootDir"
    ignoreFileOverride = "$rootDir/src/main/resources/api/.openapi-generator-ignore"
    configFile = "$rootDir/src/main/resources/api/config.json"
    globalProperties.set([
            apis: "",
            models: ""
    ])
}


dependencies {
    implementation(libs.bundles.spring)

    implementation(libs.passay)
    implementation(libs.type.safe)

    runtimeOnly(libs.h2database)
    implementation(libs.flywaydb)

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)

    // OpenAPI
    implementation(libs.bundles.openapi)

    testImplementation(testLibs.bundles.testing)
    testRuntimeOnly(testLibs.junit.platform.launcher)

}

test {
    useJUnitPlatform()
    jvmArgs '-Xshare:off'
}

compileJava.dependsOn tasks.openApiGenerate
jar.enabled(false)